import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
}

sourceSets {
    commonUtils {
        java {
            srcDirs = ['src/com/google/appinventor/common/utils/']
            // TODO: compiledBy <task>
        }
// TODO: breaks when commonUtils & commonVersion both have same resources dir
//        resources {
//            srcDirs = ['src/com/google/appinventor/common/']
//            include 'CommonUtils.gwt.xml'
//        }
    }
    commonVersion {
        java {
            srcDirs = [
                    'src/com/google/appinventor/common/version/',
                    "$buildDir/gen/src/com/google/appinventor/common/version/"
            ]
        }
// TODO: see above
//        resources {
//            srcDirs = ['src/com/google/appinventor/common/']
//            include 'CommonVersion.gwt.xml'
//        }
    }
}

dependencies {
    commonUtilsImplementation files('../lib/guava/guava-14.0.1.jar')
}

defaultTasks 'CommonUtilsJar', 'CommonUtilsGwtJar', 'CommonVersionJar', 'CommonVersionGwtJar'

task GitBuildId(type: Copy) {
    from 'GitBuildId.template'
    into "$buildDir/gen/src/com/google/appinventor/common/version/"

    filter ReplaceTokens, tokens: [
            "git.build.version"    : 'git describe --dirty'.execute().text.trim(),
            "git.build.fingerprint": 'git rev-parse HEAD'.execute().text.trim(),
            "ant.build.date"       : new Date().format('MMMM dd yyyy'),
            "acra.uri"             : ''
    ]
    filteringCharset = 'UTF-8'

    rename '(GitBuildId).template', '$1.java'
}

compileCommonVersionJava.dependsOn GitBuildId

task CommonUtilsJar(type: Jar) {
    from sourceSets.commonUtils.output

    archiveFileName = 'CommonUtils-gwt.jar'
}

task CommonUtilsGwtJar(type: Jar) {
    from sourceSets.commonUtils.output
    // TODO: all Java files are flattened in the JAR!
    from sourceSets.commonUtils.allJava
    from 'src/com/google/appinventor/common/CommonUtils.gwt.xml'

    archiveFileName = 'CommonUtils-gwt.jar'
}

task CommonVersionJar(type: Jar) {
    from sourceSets.commonVersion.output

    archiveFileName = 'CommonVersion.jar'
}

task CommonVersionGwtJar(type: Jar) {
    from sourceSets.commonVersion.output
    // TODO: see above
    from sourceSets.commonVersion.allJava
    from 'src/com/google/appinventor/common/CommonVersion.gwt.xml'

    archiveFileName = 'CommonVersion-gwt.jar'
}