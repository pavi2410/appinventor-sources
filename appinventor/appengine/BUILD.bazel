load("@aspect_bazel_lib//lib:run_binary.bzl", "run_binary")
load("@aspect_bazel_lib//lib:output_files.bzl", "output_files")
load("@aspect_bazel_lib//lib:directory_path.bzl", "directory_path")
load("@rules_pkg//:pkg.bzl", "pkg_zip")

java_library(
    name = "AiShared",
    srcs = glob(["src/com/google/appinventor/shared/**/*.java"]),
    deps = [
        "//appinventor/common:CommonUtils",
        "//appinventor/common:CommonVersion",
        "//appinventor/components:CommonConstants",
        "//appinventor/lib:WarLibs",
        #        "//appinventor/lib:LocalJarsForAiShared",
    ],
)

java_library(
    name = "AiRebind",
    srcs = glob(["src/com/google/appinventor/rebind/**/*.java"]),
    deps = [
        "//appinventor/lib:LocalJarsForGwtSdk",
    ],
)

# use config_setting to toggle dev mode
java_binary(
    name = "YaClientApp",
    args = [
        "-XdisableUpdateCheck",
        "-localWorkers",
        "8",
        "-optimize",
        "9",
        "-war",
        "war",
        "-extra",
        "extra",
        "-logLevel",
        "INFO",
        "com.google.appinventor.YaClient",
    ],
    jvm_flags = [
        "-Xss2M",
        "-Xmx2G",
        "-Dfile.encoding=UTF-8",
    ],
    main_class = "com.google.gwt.dev.Compiler",
    resource_strip_prefix = "appinventor/appengine/src",
    resources = glob([
        "src/com/google/appinventor/client/**/*.java",
        "src/com/google/appinventor/client/**/*.ui.xml",
        "src/com/google/appinventor/shared/**/*.java",
        "src/com/google/appinventor/images/*.png",
        "src/com/google/appinventor/images/*.gif",
    ]) + [
        ":GeneratedFilesFromComponents",
        ":GenerateBlocklyTranslation",
        "src/com/google/appinventor/client/languages.json",
        "src/com/google/appinventor/client/utils/html5dnd.js",
        "src/com/google/appinventor/YaClient.gwt.xml",
        ":blockly_messages_jar",
        ":BlocklyCompile",
    ],
    runtime_deps = [
        ":AiRebind",
        "//appinventor/common:CommonUtils",
        "//appinventor/common:CommonVersion",
        "//appinventor/components:CommonConstants",
        "//appinventor/lib:LocalJarsForGwtSdk",
        "//appinventor/lib:LocalJarsForYaClientApp",
    ],
)

GENERATED_FILES_SRC = "$(GENDIR)/appinventor/components/_javac/AndroidRuntime/libAndroidRuntime_sources/"

genrule(
    name = "GeneratedFilesFromComponents",
    srcs = ["//appinventor/components:AndroidRuntime"],
    outs = [
        "src/com/google/appinventor/client/ComponentsTranslation.java",
        "src/com/google/appinventor/client/AutogeneratedOdeMessages.java",
        "src/com/google/appinventor/simple_components.json",
    ],
    cmd_bash = 'mkdir -p "$(RULEDIR)/src/" && cp "{}" "$(RULEDIR)/src/"'.format(
        GENERATED_FILES_SRC,
    ),
    cmd_bat = 'xcopy /Y /I /F /E "{}" "$(RULEDIR)\\src\\"'.format(
        GENERATED_FILES_SRC.replace("/", "\\"),
    ),
    visibility = [
        "//appinventor/appengine:__pkg__",
    ],
)

AI_BLOCKLY_MSG_DIR = "$(GENDIR)/../../../appinventor/blocklyeditor/src/msg/ai_blockly"

BLOCKLY_MSG_JSON_DIR = "$(GENDIR)/../../../appinventor/lib/blockly/msg/json"

BLOCKLY_TRANSLATION_DEST = "$(RULEDIR)/src/msg"

run_binary(
    name = "GenerateBlocklyTranslation",
    args = [
        AI_BLOCKLY_MSG_DIR,
        BLOCKLY_MSG_JSON_DIR,
        BLOCKLY_TRANSLATION_DEST,
    ],
    out_dirs = ["src/msg"],
    tool = "//appinventor/blocklyeditor:BlocklyTranslationGenerator",
)

pkg_zip(
    name = "blockly_messages_zip",
    srcs = [":GenerateBlocklyTranslation"],
    package_file_name = "src/blockly_messages.jar",
)

java_import(
    name = "blockly_messages_jar",
    jars = [":blockly_messages_zip"],
)

genrule(
    name = "BlocklyCompile",
    srcs = [
        "//appinventor/blocklyeditor:ploverConfig.js",
    ],
    outs = ["src/com/google/appinventor/client/editor/youngandroid/blockly.js"],
    cmd_bash = "$(location //appinventor/lib:Plovr) build $(location //appinventor/blocklyeditor:ploverConfig.js) > $@",
    cmd_bat = "$(location //appinventor/lib:Plovr) build $(location //appinventor/blocklyeditor:ploverConfig.js) > $@",
    tools = [
        "//appinventor/lib:Plovr",
    ],
)
